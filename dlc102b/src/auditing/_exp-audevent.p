/*************************************************************/  
/* Copyright (c) 1984-2005,2007 by Progress Software Corporation  */
/*                                                           */
/* All rights reserved.  No part of this program or document */
/* may be  reproduced in  any form  or by  any means without */
/* permission in writing from PROGRESS Software Corporation. */
/*************************************************************/
/*------------------------------------------------------------------------
    File        : _exp-audevent.p
    
    Purpose     : Exports audit events from a dataset into a .d. This generates
                  .d similar to the ones generated by the dump .d in the 
                  Data Admin tool. One can only export events with 
                  event id >= 32000 so we filter other id's out.
                  The rationale is that one will only need to dump/load the
                  application-level events, and not the system ones 
                  (provided by Progress).

    Author(s)   : Fernando de Souza
    Created     : Feb 23,2005
    Notes       :
    
    History:
    
    fernando      06/20/07   Support for large files
  ----------------------------------------------------------------------*/

/*********************************************************************/
/* local definitions                                                 */
/*********************************************************************/
{auditing/ttdefs/_audeventtt.i}

DEFINE DATASET dsttAuditEvent FOR ttAuditEvent.

DEFINE VARIABLE stamp    AS CHARACTER  NO-UNDO.
DEFINE VARIABLE pos      AS INT64      NO-UNDO.
DEFINE VARIABLE cRecords AS CHARACTER  NO-UNDO.

/*********************************************************************/
/* parameters                                                        */
/*********************************************************************/
DEFINE INPUT  PARAMETER pcFileName  AS CHARACTER NO-UNDO.
DEFINE INPUT  PARAMETER DATASET     FOR dsttAuditEvent.
DEFINE OUTPUT PARAMETER pnumRecords AS INT64     NO-UNDO.


/*********************************************************************/
/* Main block                                                        */
/*********************************************************************/

OUTPUT TO VALUE(pcFileName) NO-ECHO NO-MAP.

DO ON ERROR UNDO, LEAVE.

    /* export records */
    FOR EACH ttAuditEvent WHERE _Event-id >= 32000.
        pnumRecords = pnumRecords + 1.
        EXPORT ttAuditEvent.
    END.

    /* put just some basic information in the trail section */
    ASSIGN stamp = STRING(YEAR( TODAY),"9999") + "/"
                   + STRING(MONTH(TODAY),"99"  ) + "/"
                   + STRING(DAY(  TODAY),"99"  ) + "-"
                   + STRING(TIME,"HH:MM:SS").

    PUT UNFORMATTED ".":U SKIP.

    pos = SEEK(OUTPUT).

    /* if values is too big, don't format it */
    IF pNumRecords > 9999999999999 THEN
       cRecords = STRING(pNumRecords).
    ELSE
       cRecords = STRING(pnumRecords,"9999999999999").

    PUT UNFORMATTED "PSC":U SKIP
                    "filename=_aud-event":U SKIP
                    "records=":U  cRecords SKIP
                    "timestamp=":U stamp SKIP
                    "map=NO-MAP":U SKIP
                    "cpstream=":U SESSION:CPSTREAM SKIP
                    ".":U SKIP.

    /* if values is too big, don't format it */
    IF pos > 9999999999 THEN
       PUT UNFORMATTED STRING(pos) SKIP.
    ELSE
       PUT UNFORMATTED STRING(pos,"9999999999") SKIP.

END. /* DO block */

OUTPUT CLOSE.

RETURN.
